set positional-arguments

# Display help
help:
    just -l

# Configure a local MCP server (project CODEX_HOME) from project .env
setup-mcp-local:
    bash -lc '../scripts/setup_mcp_from_env.sh --local --server search --command node --arg "$PWD/scripts/mcp_websearch.js" --env-key GOOGLE_API_KEY --env-key GOOGLE_CSE_ID --env-key SERPAPI_KEY'

# Run ocodex with project-local CODEX_HOME and MCP config
ocodex-local *args:
    CODEX_HOME="$PWD/../ocodex/.codex" cargo run --bin ocodex -- "$@"

# `codex`
codex *args:
    cargo run --bin codex -- "$@"

# `codex exec`
exec *args:
    cargo run --bin codex -- exec "$@"

# `codex tui`
tui *args:
    cargo run --bin codex -- tui "$@"

# Run the CLI version of the file-search crate.
file-search *args:
    cargo run --bin codex-file-search -- "$@"

# format code
fmt:
    cargo fmt -- --config imports_granularity=Item

fix *args:
    cargo clippy --fix --all-features --tests --allow-dirty "$@"

install:
    rustup show active-toolchain
    cargo fetch
